import app.web.diegoflassa_site.littledropsofrain.buildsrc.Versions

buildscript {
    ext.kotlin_version = Versions.kotlinCompilerVersion
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${Versions.gradle}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.gradle_plugin}"
        classpath "com.google.gms:google-services:${Versions.google_services}"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:${Versions.safeargs_plugin}"

        // Add the Crashlytics Gradle plugin.
        classpath "com.google.firebase:firebase-crashlytics-gradle:${Versions.crashlytics}"
		// Performance Monitoring plugin
        classpath "com.google.firebase:perf-plugin:${Versions.perf}"
		// Add the App Distribution Gradle plugin
		classpath "com.google.firebase:firebase-appdistribution-gradle:${Versions.app_distribution}"

        classpath "com.google.android.gms:oss-licenses-plugin:${Versions.oss_plugin}"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.diffplug.spotless" version "5.10.2"
}

subprojects {
    apply plugin: "com.diffplug.spotless"
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(Versions.ktlint).userData(['disabled_rules' : 'no-wildcard-imports'])
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }
}

allprojects {
    repositories {
        google()
		mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
     }
}